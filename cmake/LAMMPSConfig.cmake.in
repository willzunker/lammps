include(CMakeFindDependencyMacro)
if(@BUILD_MPI@)
  find_dependency(MPI REQUIRED CXX)
endif()
if(@PKG_KSPACE@)
  if(@FFT@ STREQUAL "FFTW3")
    if(@FFTW@ STREQUAL "FFTW3" AND NOT TARGET FFTW3::FFTW3)
      add_library(FFTW3::FFTW3 UNKNOWN IMPORTED)
      set_target_properties(FFTW3::FFTW3 PROPERTIES
        IMPORTED_LINK_INTERFACE_LANGUAGES "C"
        IMPORTED_LOCATION "@FFTW3_LIBRARY@"
        INTERFACE_INCLUDE_DIRECTORIES "@FFTW3_INCLUDE_DIRS@")
    endif()
    if(@FFTW@ STREQUAL "FFTW3F" AND NOT TARGET FFTW3F::FFTW3F)
      add_library(FFTW3F::FFTW3F UNKNOWN IMPORTED)
      set_target_properties(FFTW3F::FFTW3F PROPERTIES
        IMPORTED_LINK_INTERFACE_LANGUAGES "C"
        IMPORTED_LOCATION "@FFTW3F_LIBRARY@"
        INTERFACE_INCLUDE_DIRECTORIES "@FFTW3F_INCLUDE_DIRS@")
    endif()
  endif()
endif()
if(NOT @BUILD_SHARED_LIBS@)
  if(@WITH_JPEG@)
    find_dependency(JPEG REQUIRED)
  endif()
  if(@WITH_PNG@)
    find_dependency(PNG REQUIRED)
    find_dependency(ZLIB REQUIRED)
  endif()
  if(@PKG_KIM@ AND NOT @DOWNLOAD_KIM@)
    find_dependency(PkgConfig REQUIRED)
    pkg_check_modules(KIM-API REQUIRED IMPORTED_TARGET libkim-api>=@KIM-API_MIN_VERSION@)
    if(@CURL_FOUND@)
      find_dependency(CURL REQUIRED)
    endif()
  endif()
  if(@PKG_USER-SMD@ AND NOT @DOWNLOAD_EIGEN3@)
    find_dependency(Eigen3 NO_MODULE REQUIRED)
  endif()
  if(@PKG_KSPACE@)
    if(@FFT@ STREQUAL "FFTW3" AND @FFT_FFTW_THREADS@)
      if(@FFTW@ STREQUAL "FFTW3" AND NOT TARGET FFTW3::FFTW3_OMP)
        add_library(FFTW3::FFTW3_OMP UNKNOWN IMPORTED)
        set_target_properties(FFTW3::FFTW3_OMP PROPERTIES
          IMPORTED_LINK_INTERFACE_LANGUAGES "C"
          IMPORTED_LOCATION "@FFTW3_OMP_LIBRARY@"
          INTERFACE_INCLUDE_DIRECTORIES "@FFTW3_OMP_INCLUDE_DIRS@")
      endif()
      if(@FFTW@ STREQUAL "FFTW3F" AND NOT TARGET FFTW3F::FFTW3F_OMP)
        add_library(FFTW3F::FFTW3F_OMP UNKNOWN IMPORTED)
        set_target_properties(FFTW3F::FFTW3F_OMP PROPERTIES
          IMPORTED_LINK_INTERFACE_LANGUAGES "C"
          IMPORTED_LOCATION "@FFTW3F_OMP_LIBRARY@"
          INTERFACE_INCLUDE_DIRECTORIES "@FFTW3F_OMP_INCLUDE_DIRS@")
      endif()
    endif()
  endif()
  if(@PKG_USER-SCAFACOS@ AND NOT @DOWNLOAD_SCAFACOS@)
    find_dependency(PkgConfig REQUIRED)
    pkg_check_modules(SCAFACOS REQUIRED IMPORTED_TARGET scafacos)
  endif()
  if(@PKG_PYTHON@ AND NOT CMAKE_VERSION VERSION_LESS 3.12)
    find_package(Python REQUIRED COMPONENTS Development)
  endif()
  if(@PKG_COMPRESS@)
    find_dependency(ZLIB REQUIRED)
  endif()
  if(@PKG_KOKKOS@)
    if(@EXTERNAL_KOKKOS@)
      find_dependency(Kokkos 3 REQUIRED)
    endif()
  endif()
  if(@PKG_VORONOI@ AND NOT @DOWNLOAD_VORO@)
    if(NOT TARGET VORO::VORO)
      add_library(VORO::VORO UNKNOWN IMPORTED)
      set_target_properties(VORO::VORO PROPERTIES
        IMPORTED_LOCATION "@VORO_LIBRARY@"
        INTERFACE_INCLUDE_DIRECTORIES "@VORO_INCLUDE_DIR@")
    endif()
  endif()
  if(@PKG_USER-INTEL@)
    if(@INTEL_LRT_MODE@ STREQUAL "THREADS")
      find_dependency(Threads REQUIRED)
    endif()
    if(@TBB_MALLOC_FOUND@)
      if(NOT TARGET TBB::TBB_MALLOC)
        add_library(TBB::TBB_MALLOC UNKNOWN IMPORTED)
        set_target_properties(TBB::TBB_MALLOC PROPERTIES
          IMPORTED_LOCATION "@TBB_MALLOC_LIBRARY@"
          INTERFACE_INCLUDE_DIRECTORIES "@TBB_MALLOC_INCLUDE_DIR@")
      endif()
    endif()
  endif()
endif()
include("${CMAKE_CURRENT_LIST_DIR}/LAMMPS_Targets.cmake")
